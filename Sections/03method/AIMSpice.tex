\subsection{AIMSpice}
The AIMSpice part focuses on keeping the static leakage power consumption while keeping the functionality for all the corners mentioned in sec \ref{sec:Process corners} and the temperatures $0^o C$, $27^o C$ and $70^o C$. We do also want to keep in mind the timings mentioned in sec \ref{sec:Delays} as we can see from the equations \ref{eq:subthreshold_leakage}, \ref{eq:gate_capacitance} and \ref{eq:parasitic_resistance}, a change in the parameters $V_{DD}$ and $\frac{W}{L}$ both affect the static power consumption and the timing of the circuit. This way we want to find the sweetspot where we keep a low power without compensating to much on the timings.

\subsubsection{AIMSpice-implementation}
Gpdk 90nm technology were used to implement the DFF. First the logic gates were implemented as subcircuits

\begin{lstlisting}
* --------------------------------------------------------------
* 2-Input NAND Gate
* Ports: A, B (Inputs), Out (Output), Vdd (Positive Supply), Vss (Ground)
* --------------------------------------------------------------
.subckt NAND A B Out Vdd Vss

    * PMOS transistors
    XMP1 Out A Vdd Vdd pmos1v w=P_Width l=P_Length 
    XMP2 Out B Vdd Vdd pmos1v w=P_Width l=P_Length 

    * NMOS transistors
    XMN1 Out A NS1 Vss nmos1v w=N_Width l=N_Length 
    XMN2 NS1 B Vss Vss nmos1v w=N_Width l=N_Length 

.ends
\end{lstlisting}

\begin{lstlisting}
* --------------------------------------------------------------
* 3-Input NAND Gate
* Ports: InputA, InputB, InputC (Inputs), Output (Output), 
* Vdd (Positive Supply), Vss (Ground)
* --------------------------------------------------------------
.subckt NAND_3 InputA InputB InputC Output Vdd Vss

    * PMOS transistors
    XMP1 Output InputA Vdd Vdd pmos1v w=P_Width l=P_Length 
    XMP2 Output InputB Vdd Vdd pmos1v w=P_Width l=P_Length
    XMP3 Output InputC Vdd Vdd pmos1v w=P_Width l=P_Length

    * NMOS transistors
    XMN1 Output InputA N1 Vss nmos1v w=N_Width l=N_Length 
    XMN2 N1 InputB N2 Vss nmos1v w=N_Width l=N_Length
    XMN3 N2 InputC Vss Vss nmos1v w=N_Width l=N_Length  

.ends
\end{lstlisting}

\begin{lstlisting}
* --------------------------------------------------------------
* DFF with asynchronous reset circuit
* Ports: CLK, InputData, Reset, Output, Vdd, Vss
* --------------------------------------------------------------
.subckt DFF CLK D Res Output NandOut3 Vdd Vss

    * 2-Input NAND Gate
    XN21 NandOut1 SetNode NandOut2 Vdd Vss NAND
    XN22 SetNode NandOut3 Output Vdd Vss NAND

    * 3-Input NAND Gate
    XN31 NandOut2 CLK Res SetNode Vdd Vss NAND_3 
    XN32 SetNode CLK NandOut1 ResetNode Vdd Vss NAND_3 
    XN33 ResetNode D Res NandOut1 Vdd Vss NAND_3 
    XN34 Output ResetNode Res NandOut3 Vdd Vss NAND_3 

.ends
\end{lstlisting}

The mobility of the NMOS and PMOS are diffrent, therfore the subcircuit were implemented with an option to edit the parameters for both $\frac{W_N}{L_N}$ and $\frac{W_P}{L_P}$.


\begin{lstlisting}
* param.cir:

* Global parameters
.include gpdk90nm_#1.cir
.param vdd_value = #2
.option temp= #4

* Device parameters for N-MOSFETs
.param N_Width= #5
.param N_Length = #6


* Device parameters for P-MOSFETs
.param P_Width= #7
.param P_Length = #8
\end{lstlisting}

The fuctionality were simulated using three different wave pulses to show how the DFF samples alternating data at rising edge, how it samples data when data stays the same for a few clock cycles and lastly the reset functionality:

\begin{lstlisting}
*waveW1:

*Waveform 1: To Test Data Sampling and Clock Edge
vclk Clk 0 pulse (0 vdd_value 5n 0.1n 0.1n 10n 20n)     
vres Reset 0 pulse (0 vdd_value 0 0.1n 0.1n 39n 39n)       
vd  Data 0 pulse (0 vdd_value 10n 0.1n 0.1n 20n 40n)
.tran 0.0001n 200n 10n
\end{lstlisting}

\begin{lstlisting}
*waveW2:

*Waveform 2: To Test the Datasampling when Data stays the same for a few Clock Edges
vclk Clk 0 pulse (0 vdd_value 5n 0.1n 0.1n 10n 20n)     
vres Reset 0 pulse (0 vdd_value 0 0.1n 0.1n 0n 0n)       
vd  Data 0 pulse (0 vdd_value 10n pRiseT pFallT 35n 80n)     
.tran 0.0001n 200n 10n
\end{lstlisting}

\begin{lstlisting}
*waveW3:

*Waveform 3: To Test the Synchronous Reset
vclk Clk 0 pulse (0 vdd_value 5n 0.1n 0.1n 10n 20n)     
vres Reset 0 pulse (vdd_value 0 5n 0.1n 0.1n 10n 40n)       
vd  Data 0 pulse (0 vdd_value 10n pRiseT pFallT 40n 40n)
.tran 0.0001n 200n 10n
\end{lstlisting}

